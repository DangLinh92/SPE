SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROC [dbo].[PKG_BUSINESS_SP_INVENTORY@REPORT_BY_SPAREPART_IN_OUT](
@A_DEPARTMENT		NVARCHAR(50),
@A_TIME_FROM        NVARCHAR(50),
@A_TIME_TO          NVARCHAR(50),
@N_RETURN			int				OUTPUT,
@V_RETURN			NVARCHAR(4000)	OUTPUT
)
AS
BEGIN TRY
		BEGIN
		    SET NOCOUNT OFF;  
			DECLARE @PACK_UNIT NVARCHAR(50) = 'PACK';
			DECLARE @SQL NVARCHAR(MAX);

			DECLARE @SOURCE [dbo].[TableType];

			INSERT INTO @SOURCE
			SELECT 
			    --ROW_NUMBER() OVER(ORDER BY inv.SPARE_PART_CODE ASC) as STT,
			    invtime.[MONTH],
				invtime.[YEAR],
				inv.SPARE_PART_CODE AS CODE, 
				sp.NAME_VI,
				sp.SPECIFICATIONS,
				price.PRICE_VN/[dbo].[CONVERT_UNIT](price.[UNIT_CODE],@PACK_UNIT,price.SPARE_PART_CODE,@A_DEPARTMENT) as PRICE_VN,
				price.PRICE_US/[dbo].[CONVERT_UNIT](price.[UNIT_CODE],@PACK_UNIT,price.SPARE_PART_CODE,@A_DEPARTMENT) as PRICE_US,
				v.NAME as VENDER_NAME,
				'Pack' AS UNIT,
				sp_type.NAME as SPARE_PART_TYPE,
				[dbo].[CONVERT_UNIT](mins.UNIT_CODE,@PACK_UNIT,inv.SPARE_PART_CODE,@A_DEPARTMENT)*mins.MIN_STOCK as MIN_STOCK,
				hisInv.QUANTITY_MONTH,
				hisInv.IN_OUT,
				[dbo].[CONVERT_UNIT](invtime.UNIT,@PACK_UNIT,invtime.SPARE_PART_CODE,@A_DEPARTMENT)*invtime.QUANTITY AS QUANTITY,
				[dbo].[CONVERT_UNIT](invtime.UNIT,@PACK_UNIT,invtime.SPARE_PART_CODE,@A_DEPARTMENT)*invtime.QUANTITY_REAL AS QUANTITY_REAL,
				inv.STOCK_CODE
			FROM [dbo].[EWIP_SPAREPART_INVENTORY] inv
			INNER JOIN [dbo].[EWIP_SPARE_PART] sp ON inv.SPARE_PART_CODE = sp.CODE AND sp.SP_DEPT_CODE = inv.DEPT_CODE
			LEFT JOIN [dbo].[EWIP_SETUP_MINSTOCK] mins ON inv.SPARE_PART_CODE = mins.SPARE_PART_CODE AND mins.DEPT_CODE = @A_DEPARTMENT
			LEFT JOIN [dbo].[EWIP_SPAREPART_TYPE] sp_type ON sp.TYPE = sp_type.CODE --AND sp_type.DEPT_CODE = @A_DEPARTMENT
			LEFT JOIN [dbo].[EWIP_VENDER] v ON sp.VENDER_ID = v.VENDER_ID
			LEFT JOIN  (
			            SELECT TOP 1 WITH TIES 
						[SPARE_PART_CODE],
						[PRICE_VN],
						[PRICE_US], 
						[UNIT_CODE]
						FROM [dbo].[EWIP_SPAREPART_PRICE]
						WHERE [DEPT_CODE] = @A_DEPARTMENT --AND [DATE] BETWEEN CAST(@A_TIME_FROM AS date) AND CAST(@A_TIME_TO AS date)
						ORDER BY ROW_NUMBER() OVER(PARTITION BY SPARE_PART_CODE ORDER BY [DATE] DESC)
						) AS price ON inv.SPARE_PART_CODE = price.SPARE_PART_CODE
            LEFT JOIN [dbo].[EWIP_INVENTORY_BY_TIME] invtime ON inv.SPARE_PART_CODE = invtime.SPARE_PART_CODE AND invtime.DEPT_CODE = inv.DEPT_CODE
			LEFT JOIN  
			(
				SELECT [SPARE_PART_CODE],[STOCK_CODE],[YEAR],[MONTH],[IN_OUT],SUM([dbo].[CONVERT_UNIT]([UNIT],@PACK_UNIT,SPARE_PART_CODE,@A_DEPARTMENT)*[QUANTITY]) AS QUANTITY_MONTH
				FROM [dbo].[EWIP_HISTORY_INVENTORY] 
				WHERE [DEPT_CODE] = @A_DEPARTMENT AND [DATE] BETWEEN CAST(@A_TIME_FROM AS date) AND CAST(@A_TIME_TO AS date)
				GROUP BY [STOCK_CODE],[SPARE_PART_CODE],[YEAR],[MONTH],IN_OUT
			) as hisInv ON inv.SPARE_PART_CODE = hisInv.SPARE_PART_CODE AND invtime.MONTH= hisInv.MONTH AND invtime.YEAR = hisInv.YEAR
			WHERE inv.DEPT_CODE = @A_DEPARTMENT

			DECLARE @SUM_ALL_ARR NVARCHAR(MAX) = [dbo].[SUM_ALL_ARR](@A_TIME_FROM,@A_TIME_TO);
			DECLARE @ALL_ARR NVARCHAR(MAX) = [dbo].[ALL_ARR](@A_TIME_FROM,@A_TIME_TO);
			DECLARE @RECEIVE_ARR NVARCHAR(MAX) = [dbo].[RECEIVE_ARR](@A_TIME_FROM, @A_TIME_TO);
			DECLARE @DELIVERY_ARR NVARCHAR(MAX) = [dbo].[DELIVERY_ARR](@A_TIME_FROM, @A_TIME_TO);
			DECLARE @STOCK_ARR NVARCHAR(MAX) = [dbo].[STOCK_ARR](@A_TIME_FROM, @A_TIME_TO);
			DECLARE @INVENTORY_ARR NVARCHAR(MAX) = [dbo].[INVENTORY_ARR](@A_TIME_FROM, @A_TIME_TO);

			SET @SQL = N'
					 SELECT 
						ROW_NUMBER() OVER (ORDER BY CODE) AS STT,CODE,NAME_VI,[DESCRIPTION],PRICE_VN,PRICE_US,VENDER_NAME,UNIT,SPARE_PART_TYPE,STOCK_CODE,MIN_STOCK,'+@SUM_ALL_ARR+'
					FROM
						(
						  SELECT CODE,NAME_VI,[DESCRIPTION],PRICE_VN,PRICE_US,VENDER_NAME,UNIT,SPARE_PART_TYPE,STOCK_CODE,MIN_STOCK,'+@ALL_ARR+'
						  FROM 
							(SELECT 
								CODE,NAME_VI,[DESCRIPTION],PRICE_VN,PRICE_US,VENDER_NAME,UNIT,SPARE_PART_TYPE,STOCK_CODE,MIN_STOCK,
								QUANTITY_MONTH,[dbo].[CONVERT_INOUT_STR](IN_OUT)+''_''+[dbo].[CONVERT_MONTH_STR]([MONTH])+''_''+CAST([YEAR] AS NVARCHAR(4)) AS T1,
								QUANTITY,[dbo].[CONVERT_INOUT_STR](''QUANTITY'')+''_''+[dbo].[CONVERT_MONTH_STR]([MONTH])+''_''+CAST([YEAR] AS NVARCHAR(4)) AS T2,
								QUANTITY_REAL,[dbo].[CONVERT_INOUT_STR](''QUANTITY_REAL'')+''_''+[dbo].[CONVERT_MONTH_STR]([MONTH])+''_''+CAST([YEAR] AS NVARCHAR(4)) AS T3
							  FROM @SOURCE
							 ) AS  SOURCE_DATA
							 PIVOT 
							 (
								SUM(QUANTITY_MONTH) FOR T1 IN('+@RECEIVE_ARR+','+@DELIVERY_ARR+')
							 ) AS PV1

							 PIVOT 
							 (
								SUM(QUANTITY) FOR T2 IN('+@STOCK_ARR+')
							 ) AS PV2
							 
							 PIVOT 
							 (
								SUM(QUANTITY_REAL) FOR T3 IN('+@INVENTORY_ARR+')
							 ) AS PV3
						) AS SOURCE_DATA1
					 GROUP BY STOCK_CODE,CODE,NAME_VI,[DESCRIPTION],PRICE_VN,PRICE_US,VENDER_NAME,UNIT,SPARE_PART_TYPE,MIN_STOCK';
			
			PRINT (@SQL)
            EXEC sp_executesql 
						@stmt = @SQL
						,@params = N'@SOURCE TableType readonly'
						,@SOURCE = @SOURCE;
		END

	SET @N_RETURN = 0;
	SET @V_RETURN = 'MSG_COM_004';
END TRY
	BEGIN CATCH
  SET @N_RETURN = ERROR_NUMBER();
  SET @V_RETURN = ERROR_MESSAGE();
END CATCH

GO
